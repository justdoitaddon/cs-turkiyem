import com.lagradost.cloudstream3.gradle.CloudstreamExtension
import com.android.build.gradle.BaseExtension

buildscript {
    repositories {
        google()
        mavenCentral()
        maven("https://jitpack.io") {
            metadataSources { artifact() }
            content {
                includeModule("com.github.recloudstream.gradle", "gradle")
            }
        }
        maven("https://jitpack.io") {
            content {
                excludeModule("com.github.recloudstream.gradle", "gradle")
            }
        }
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.7.3")
        classpath("com.github.recloudstream.gradle:gradle:master-81b1d424d2-1")
        classpath("org.ow2.asm:asm:9.4")
        classpath("org.ow2.asm:asm-tree:9.4")
        classpath("com.github.vidstige:jadb:v1.2.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:2.1.0")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven("https://jitpack.io")
    }
}

fun Project.cloudstream(configuration: CloudstreamExtension.() -> Unit) = extensions.getByName<CloudstreamExtension>("cloudstream").configuration()

fun Project.android(configuration: BaseExtension.() -> Unit) = extensions.getByName<BaseExtension>("android").configuration()

subprojects {
    apply(plugin = "com.android.library")
    apply(plugin = "kotlin-android")
    apply(plugin = "com.lagradost.cloudstream3.gradle")

    cloudstream {
        setRepo(System.getenv("GITHUB_REPOSITORY") ?: "https://github.com/keyiflerolsun/Kekik-cloudstream")
        authors = listOf("keyiflerolsun")
    }

    android {
        namespace = "com.keyiflerolsun"

        defaultConfig {
            minSdk = 21
            compileSdkVersion(35)
            targetSdk = 35
        }

        lintOptions { isAbortOnError = false }

        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_11
            targetCompatibility = JavaVersion.VERSION_11
        }

        tasks.withType<org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile> {
            compilerOptions {
                jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_11)
                freeCompilerArgs.addAll(
                    listOf(
                        "-Xno-call-assertions",
                        "-Xno-param-assertions",
                        "-Xno-receiver-assertions",
                        "-Xskip-metadata-version-check"
                    )
                )
            }
        }
    }

    dependencies {
        val cloudstream by configurations
        val implementation by configurations

        cloudstream("com.lagradost:cloudstream3:pre-release")

        implementation(kotlin("stdlib"))                                              // Kotlin'in temel kǬtǬphanesi
        implementation("com.github.Blatzar:NiceHttp:0.4.13")                          // HTTP kǬtǬphanesi
        implementation("org.jsoup:jsoup:1.19.1")                                      // HTML ayrYtrc
        implementation("com.fasterxml.jackson.module:jackson-module-kotlin:2.13.1")   // Kotlin iin Jackson JSON kǬtǬphanesi
        implementation("com.fasterxml.jackson.core:jackson-databind:2.16.0")          // JSON-nesne dnǬYtǬrme kǬtǬphanesi
        implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.1")      // Kotlin iin asenkron iYlemler
        implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.0")
        implementation("com.github.vidstige:jadb:v1.2.1")
    }
}

task<Delete>("clean") {
    delete(rootProject.layout.buildDirectory)
}


